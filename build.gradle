import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id 'net.minecraftforge.licenser' version '1.0.1'
  id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
  id 'com.github.johnrengelman.shadow' version '8.1.1'
  id 'maven-publish'
  id 'java'
  id 'eclipse'
  id 'idea'
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
}

dependencies {
    implementation(libs.bundles.asm)
    implementation(libs.jopt.simple)
    implementation(libs.gson)
    implementation(libs.mergetool)
    implementation(libs.srgutils)
}

group = 'net.minecraftforge.lex'
version = gradleutils.tagOffsetVersion

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

license {
  header project.file('LICENSE-header.txt')
  newLine false
}

compileJava {
    options.encoding = 'UTF-8'
}

artifacts {
    archives shadowJar
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes('Main-Class': 'net.minecraftforge.lex.mappingtoy.MappingToy')
        attributes([
            'Specification-Title':    project.name,
            'Specification-Vendor':  'Forge Development LLC',
            'Specification-Version':  gradleutils.gitInfo.tag,
            'Implementation-Title':   project.name,
            'Implementation-Version': project.version,
            'Implementation-Vendor': 'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/lex/mappingtoy/')
    }
}

tasks.named('shadowJar', ShadowJar).configure {
    archiveClassifier = 'all'
    minimize()
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        artifact sourcesJar
        artifact jar
        artifact shadowJar
        pom {
            name = project.archivesBaseName
            packaging = 'jar'
            
            gradleutils.pom.setGitHubDetails(pom, 'LexManos', 'MappingToy')
            
            license gradleutils.pom.licenses.LGPLv2_1

            developers {
                developer gradleutils.pom.Developers.LexManos
            }
        }
    }
    
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

changelog {
    from "f753175b79b648af8c84f4df3122ecfaf9ea5870"
}