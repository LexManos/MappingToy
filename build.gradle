plugins {
  id 'com.github.ben-manes.versions' version '0.39.0'
  id 'org.cadixdev.licenser' version '0.6.1'
  id 'net.minecraftforge.gradleutils' version '1.+'
  id 'com.github.johnrengelman.shadow' version '7.0.0'
  id 'maven-publish'
  id 'java'
  id 'eclipse'
  id 'idea'
}

ext {
    ASM_VERSION = '9.2'
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.minecraftforge.net/' }
}

dependencies {
    implementation "org.ow2.asm:asm:${ASM_VERSION}"
    implementation "org.ow2.asm:asm-commons:${ASM_VERSION}"
    implementation "org.ow2.asm:asm-tree:${ASM_VERSION}"
    implementation "org.ow2.asm:asm-util:${ASM_VERSION}"
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'net.minecraftforge:mergetool:1.1.4'
    implementation 'net.minecraftforge:srgutils:0.4.3'
}

ext {
    MAIN_CLASS = 'net.minecraftforge.lex.mappingtoy.MappingToy'
    GIT_REPO = 'LexManos/MappingToy'
    SPEC_VERSION = gradleutils.gitInfo.tag
}

group = 'net.minecraftforge.lex'
version = gradleutils.getTagOffsetBranchVersion()
java.toolchain.languageVersion = JavaLanguageVersion.of(8)

license {
  header project.file('LICENSE-header.txt')
  newLine false
}

ext {
    MANIFEST = manifest{
        if (MAIN_CLASS != null)
            attributes('Main-Class': MAIN_CLASS)
        attributes([
            'Specification-Title':      project.name,
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    SPEC_VERSION,
            'Implementation-Title':     project.name,
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, project.group.replaceAll('\\.', '/') + '/' + project.name + '/')
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    manifest.from(MANIFEST)
}

artifacts {
    archives sourcesJar
    archives shadowJar
}

jar {
    manifest.from(MANIFEST)
}

shadowJar {
    minimize()
    classifier 'all'
    manifest.from(MANIFEST)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact sourcesJar
            artifact jar
            artifact shadowJar
            pom {
                name = project.archivesBaseName
                packaging = 'jar'
                url = "https://github.com/${GIT_REPO}"

                scm {
                    url = "https://github.com/${GIT_REPO}"
                    connection = "scm:git:git://github.com/${GIT_REPO}.git"
                    developerConnection = "scm:git:git@github.com:${GIT_REPO}.git"
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/${GIT_REPO}/issues"
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'Lex Manos'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (System.env.MAVEN_USER) {
                url 'https://maven.minecraftforge.net/'
                authentication {
                    basic(BasicAuthentication)
                }
                credentials {
                    username = System.env.MAVEN_USER ?: 'not'
                    password = System.env.MAVEN_PASSWORD ?: 'set'
                }
            } else {
                url 'file://' + rootProject.file('repo').getAbsolutePath()
            }
        }
    }
}
